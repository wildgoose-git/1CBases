#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//TODO: Вставить содержимое обработчика
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьСлужебныеРеквизиты();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ЗаполнитьСлужебныеРеквизиты();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРеквизитовФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ОбщегоНазначенияКлиент.ЗаполнитьДатыИзПериода(Период,Объект.ДатаНачала,Объект.ДатаОкончания);
КонецПроцедуры
	
#КонецОбласти 

#Область ОбработчикиРеквизитовТабличнойЧасти	

&НаКлиенте
Процедура СоставРаботПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборСтрокОбъектов();
	
КонецПроцедуры

&НаКлиенте
Процедура СоставРаботЗадачаПриИзменении(Элемент)
	УстановитьИндексСоставаРабот();
КонецПроцедуры

&НаКлиенте
Процедура СоставРаботОписаниеРешенияПриИзменении(Элемент)
	УстановитьИндексСоставаРабот();
КонецПроцедуры

&НаКлиенте
Процедура СоставРаботПередУдалением(Элемент, Отказ)

	Для каждого Идентификатор Из Элементы.СоставРабот.ВыделенныеСтроки Цикл
	
		СтрокаПоиска = Объект.СоставРабот.НайтиПоИдентификатору(Идентификатор);
		
		МассивСтрок = Объект.ОбъектКонфигурации.НайтиСтроки(Новый Структура("ИндексСтрокиСоставаРабот",СтрокаПоиска.ИндексСтроки));
		Для каждого СтрокаОбъектов Из  МассивСтрок Цикл
			Объект.ОбъектКонфигурации.Удалить(Объект.ОбъектКонфигурации.Индекс(СтрокаОбъектов))
		КонецЦикла;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставРаботПериодПриИзменении(Элемент)
	
	//ИдентификаторСтроки = Элементы.СоставРабот.ВыделенныеСтроки[0];
	//СтрокаОбъекта = Объект.СоставРабот.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	ТекущиеДанные = Элементы.СоставРабот.ТекущиеДанные;
	ОбщегоНазначенияКлиент.ЗаполнитьДатыИзПериода(ТекущиеДанные.Период,ТекущиеДанные.ДатаНачала,ТекущиеДанные.ДатаОкончания);
	
КонецПроцедуры

#КонецОбласти    

#Область СлужебныеПроцедурыИФункции


&НаКлиенте
Функция ПолучитьНовыйИндекс(ДанныеКоллекция)
	
	Индекс = 0;
	Для каждого СтрокаТаблицы Из ДанныеКоллекция Цикл
		Индекс = Макс(СтрокаТаблицы.ИндексСтроки,Индекс)
	КонецЦикла;
	
	Возврат Индекс + 1;
	
КонецФункции // ПолучитьНовыйИндекс()

&НаКлиенте
Процедура ОбъектКонфигурацииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если ИндексСтрокиСоставаРабот = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Не выбрана строка работ'; en = 'Job line not selected'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектКонфигурацииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если  Не ОтменаРедактирования Тогда
	   	Для каждого СтрокаТаблицы Из Объект.ОбъектКонфигурации Цикл       
		
			 Если СтрокаТаблицы.ИндексСтрокиСоставаРабот = 0 Тогда
			 	 СтрокаТаблицы.ИндексСтрокиСоставаРабот = ИндексСтрокиСоставаРабот
			 КонецЕсли;
		
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборСтрокОбъектов()

	ТекущиеДанные = Элементы.СоставРабот.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ИндексСтрокиСоставаРабот = Неопределено;
		Элементы.ОбъектКонфигурации.ОтборСтрок = Неопределено;	
	Иначе
		ИндексСтрокиСоставаРабот =  ТекущиеДанные.ИндексСтроки;
		Элементы.ОбъектКонфигурации.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("ИндексСтрокиСоставаРабот",ИндексСтрокиСоставаРабот));	
	КонецЕсли; 


КонецПроцедуры // УстановитьОтборСтрокОбъектов()

&НаКлиенте
Процедура УстановитьИндексСоставаРабот()

	ТекущиеДанные = Элементы.СоставРабот.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ТекущиеДанные.ИндексСтроки = 0 Тогда
		
		ИндексСтрокиСоставаРабот = ПолучитьНовыйИндекс(Объект.СоставРабот);
		ТекущиеДанные.ИндексСтроки =  ИндексСтрокиСоставаРабот;
		
		УстановитьОтборСтрокОбъектов();

	КонецЕсли;


КонецПроцедуры // УстановитьИндексСоставаРабот()


&НаКлиенте
Процедура ЗаполнитьСлужебныеРеквизиты()

	ОбщегоНазначенияКлиент.ЗаполнитьПериодИзДаты(Период,Объект.ДатаНачала,Объект.ДатаОкончания);
	
	Для каждого СтрокаТаблицы Из Объект.СоставРабот Цикл
		ОбщегоНазначенияКлиент.ЗаполнитьПериодИзДаты(СтрокаТаблицы.Период,СтрокаТаблицы.ДатаНачала,СтрокаТаблицы.ДатаОкончания);
	КонецЦикла;


КонецПроцедуры // ЗаполнитьСлужебныеРеквизиты()

	
#КонецОбласти 